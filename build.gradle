plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
}

dependencies {
    api(
        // For getting the Cognito UserPool Group Description, reading YAML, and accessing SSM
        libs.softwareAmazonAwssdk.cognitoidentityprovider,
        libs.comFasterxmlJacksonDataformat.jacksonDataformatYaml,
        libs.softwareAmazonAwssdk.ssm,
        // For SAML generation, from Keycloak
        libs.orgKecloak.keycloakSamlCore,
        libs.orgKecloak.keycloakServices,
        libs.orgBouncycastle.bcprovJdk18on,
        libs.orgBouncycastle.bcpkixJdk18on,
        // For the AWS CDK
        libs.softwareAmazonAwscdk.awsCdkLib,
        libs.softwareConstructs.constructs
    )
    implementation(
        libs.comAmazonaws.awsLambdaJavaCore,
        libs.comAmazonaws.awsLambdaJavaEvents,
        libs.orgApacheLoggingLog4j.log4jCore,
        libs.orgApacheLoggingLog4j.log4jApi,
        libs.orgJetbrains.annotations
    )
    runtimeOnly libs.comAmazonaws.awsLambdaJavaLog4j2
    testImplementation(
        libs.orgJunitJupiter.junitJupiter,
        libs.orgJunitJupiter.junitJupiterApi,
        libs.orgJunitJupiter.junitJupiterParams,
        libs.orgMockito.mockitoCore
    )
    testRuntimeOnly libs.orgJunitPlatform.junitPlatformLauncher
}

test {
    jvmArgs "-Xshare:off"
    useJUnitPlatform()
}

// Task for building the zip file for upload
tasks.register('buildZip', Zip) {
    // Using the Zip API from gradle to build a zip file of all the dependencies
    // Link: https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Zip.html

    // set the base name of the zip file
    archiveBaseName = "customIdp"
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

tasks.register('awscdk', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "gov.nj.innovation.customAwsIdp.awscdk.AwsIdpCdkApp"
}

build.dependsOn buildZip

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}
