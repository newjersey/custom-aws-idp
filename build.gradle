plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
}

dependencies {
    api(
        // From Serverless
        'com.amazonaws:aws-lambda-java-core:1.2.3',
        'com.amazonaws:aws-lambda-java-log4j:1.0.1',
        'com.fasterxml.jackson.core:jackson-core:2.15.2',
        'com.fasterxml.jackson.core:jackson-databind:2.15.2',
        'com.fasterxml.jackson.core:jackson-annotations:2.15.2',
        // For SAML generation
        'org.keycloak:keycloak-saml-core:22.0.3',
        'org.keycloak:keycloak-adapter-core:22.0.3',
        'org.keycloak:keycloak-services:22.0.3'
    )
    testImplementation(
        'org.junit.jupiter:junit-jupiter-api:5.10.0',
        'org.junit.jupiter:junit-jupiter-params:5.10.0',
        'uk.org.webcompere:system-stubs-jupiter:2.1.3'
    )
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

// Provide env vars for KeyConstants
test {
    environment([
        "KEY_PRIVATE_EXPONENT": "123",
        "KEY_PRIME_P": "123",
        "KEY_PRIME_Q": "123",
        "KEY_PRIME_EXPONENT_P": "123",
        "KEY_PRIME_EXPONENT_Q": "123",
        "KEY_CRT_COEFFICIENT": "123",
        "DEFAULT_SESSION_DURATION": 900
    ])
    useJUnitPlatform()
}

// Task for building the zip file for upload
task buildZip(type: Zip) {
    // Using the Zip API from gradle to build a zip file of all the dependencies
    //
    // The path to this zip file can be set in the serverless.yml file for the
    // package/artifact setting for deployment to the S3 bucket
    //
    // Link: https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Zip.html

    // set the base name of the zip file
    archiveBaseName = "customIdp"
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

build.dependsOn buildZip
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16
